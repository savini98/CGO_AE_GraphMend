# Scale -- Data & Storage

> Part of the [Scale subsystem](jac-scale.md).

## Storage

Jac provides a built-in storage abstraction for file and blob operations. The core runtime ships with a local filesystem implementation, and jac-scale can override it with cloud storage backends -- all through the same `store()` builtin.

### The `store()` Builtin

The recommended way to get a storage instance is the `store()` builtin. It requires no imports and is automatically backed by the active persistence provider (jac-scale when installed, otherwise core's local storage):

```jac
# Get a storage instance (no imports needed)
glob storage = store();

# With custom base path
glob storage = store(base_path="./uploads");

# With all options
glob storage = store(base_path="./uploads", create_dirs=True);
```

| Parameter | Type | Default | Description |
|-----------|------|---------|-------------|
| `base_path` | `str` | `"./storage"` | Root directory for all files |
| `create_dirs` | `bool` | `True` | Create base directory if it doesn't exist |

Without jac-scale, `store()` returns a `LocalStorage` instance. With jac-scale installed, it returns a configuration-driven backend (reading from `jac.toml` and environment variables).

### Storage Interface

All storage instances provide these methods:

| Method | Signature | Description |
|--------|-----------|-------------|
| `upload` | `upload(source, destination, metadata=None) -> str` | Upload a file (from path or file object) |
| `download` | `download(source, destination=None) -> bytes\|None` | Download a file (returns bytes if no destination) |
| `delete` | `delete(path) -> bool` | Delete a file or directory |
| `exists` | `exists(path) -> bool` | Check if a path exists |
| `list_files` | `list_files(prefix="", recursive=False)` | List files (yields paths) |
| `get_metadata` | `get_metadata(path) -> dict` | Get file metadata (size, modified, created, is_dir, name) |
| `copy` | `copy(source, destination) -> bool` | Copy a file within storage |
| `move` | `move(source, destination) -> bool` | Move a file within storage |
| `get_url` | `get_url(path, expires_in=3600) -> str` | Get a public or pre-signed URL for a file |

### Usage Example

```jac
import from jaclang.server.serving.datatypes { UploadFile }
import from uuid { uuid4 }

glob storage = store(base_path="./uploads");

walker :pub upload_file {
    has file: UploadFile;
    has folder: str = "documents";

    can process with Root entry {
        unique_name = f"{uuid4()}.dat";
        path = f"{self.folder}/{unique_name}";

        # Upload file
        storage.upload(self.file.file, path);

        # Get metadata
        metadata = storage.get_metadata(path);

        report {
            "success": True,
            "storage_path": path,
            "size": metadata["size"]
        };
    }
}

walker :pub list_files {
    has folder: str = "documents";
    has recursive: bool = False;

    can process with Root entry {
        files = [];
        for path in storage.list_files(self.folder, self.recursive) {
            metadata = storage.get_metadata(path);
            files.append({
                "path": path,
                "size": metadata["size"],
                "name": metadata["name"]
            });
        }
        report {"files": files};
    }
}
```

### S3-Compatible Cloud Storage

`jac-scale` enables seamless integration with S3-compatible object storage. When configured, the `store()` builtin returns an `S3Storage` instance instead of the default local one.

#### Configuration

Storage is configured in `jac.toml` under the `[scale.storage]` section or via environment variables.

| `jac.toml` key | Env Variable | Description | Default |
|----------------|--------------|-------------|---------|
| `type` | `JAC_STORAGE_TYPE` | Storage backend: `local` or `s3` | `local` |
| `bucket` | `JAC_STORAGE_S3_BUCKET` | S3 bucket name | None |
| `region` | `JAC_STORAGE_S3_REGION` | S3 region | `us-east-1` |
| `prefix` | `JAC_STORAGE_S3_PREFIX` | Optional prefix (directory) for all keys | `""` |
| `endpoint_url`| `JAC_STORAGE_S3_ENDPOINT_URL` | Custom endpoint for non-AWS providers | None |
| `public_read` | `JAC_STORAGE_S3_PUBLIC_READ` | If `true`, returns direct public URLs | `false` |

**Example `jac.toml`:**

```toml
[scale.storage]
type = "s3"
bucket = "my-app-uploads"
region = "us-east-1"
public_read = false
```

### Generating URLs

The `get_url()` method provides a standardized way to expose files to the internet or internal services.

- **LocalStorage**: Returns a `file://` URI to the absolute path of the file.
- **S3Storage (Private)**: Returns a secure **pre-signed URL** that expires after the specified time (default: 1 hour).
- **S3Storage (Public)**: If `public_read = true`, returns a direct, permanent public URL.

```jac
with entry {
    storage = store();

    # Generate a URL that expires in 10 minutes (600 seconds)
    # For S3, this is a pre-signed URL.
    url = storage.get_url("profile-photos/user1.jpg", expires_in=600);
}

walker :pub download_file {
    has path: str;

    can process with Root entry {
        if not storage.exists(self.path) {
            report {"error": "File not found"};
            return;
        }
        content = storage.download(self.path);
        report {"content": content, "size": len(content)};
    }
}
```

### Configuration

Configure storage in `jac.toml`:

```toml
[storage]
type = "local"           # Storage backend type
base_path = "./storage"  # Base directory for files
create_dirs = true       # Auto-create directories
```

| Option | Type | Default | Description |
|--------|------|---------|-------------|
| `type` | string | `"local"` | Storage backend (`local`, `s3`) |
| `base_path` | string | `"./storage"` | Base path for file storage |
| `create_dirs` | boolean | `true` | Automatically create directories |

**Environment Variables:**

| Variable | Description |
|----------|-------------|
| `JAC_STORAGE_TYPE` | Storage type (overrides jac.toml) |
| `JAC_STORAGE_PATH` | Base directory (overrides jac.toml) |
| `JAC_STORAGE_CREATE_DIRS` | Auto-create directories (`"true"`/`"false"`) |

Configuration priority: environment variables > `jac.toml` > defaults.

### StorageFactory (Advanced)

For advanced use cases, you can use `StorageFactory` directly instead of the `store()` builtin:

```jac
import from jaclang.scale.storage.factory { StorageFactory }

# Create with explicit type and config
glob config = {"base_path": "./my-files", "create_dirs": True};
glob storage = StorageFactory.create("local", config);

# Create using jac.toml / env var / defaults
glob default_storage = StorageFactory.get_default();
```

---

## Graph Traversal API

### Traverse Endpoint

```bash
POST /traverse
```

### Parameters

| Parameter | Type | Description | Default |
|-----------|------|-------------|---------|
| `source` | str | Starting node/edge ID | root |
| `depth` | int | Traversal depth | 1 |
| `detailed` | bool | Include archetype context | false |
| `node_types` | list | Filter by node types | all |
| `edge_types` | list | Filter by edge types | all |

### Example

```bash
curl -X POST http://localhost:8000/traverse \
  -H "Authorization: Bearer <token>" \
  -H "Content-Type: application/json" \
  -d '{
    "depth": 3,
    "node_types": ["User", "Post"],
    "detailed": true
  }'
```

---

## Async Walkers

```jac
walker async_processor {
    has items: list;

    async can process with Root entry {
        results = [];
        for item in self.items {
            result = await process_item(item);
            results.append(result);
        }
        report results;
    }
}
```

---

## Event Streaming

Optional event-streaming broker for emitting and consuming events between jac code and external systems. Off by default. Provides durable log, consumer groups, replayable offsets via `start_from`, and at-least-once delivery with retries and a DLQ.

Two implementations ship in-tree:

- **`LocalEventStream`** (in-memory): single-process, no persistence. Used automatically when no database URL is configured. Right for dev workstations, tests, and single-pod deployments.
- **`PgEventStream`** (Postgres): durable, cross-pod, backed by the same database that holds the graph. Used automatically when a database URL resolves (`[scale.events].url`, `[scale.database].url`, or `JAC_DB_URL`).

You don't pick the broker; selection happens at startup based on what's available.

### Enabling

Add the section to `jac.toml`. Master switch is `enabled`; everything else has working defaults.

```toml
[scale.events]
enabled = true
# Optional. If unset, falls back to [scale.database].url / JAC_DB_URL; if
# neither resolves, the in-memory LocalEventStream is used.
url = "postgresql://user:pass@localhost:5432/jac"
consumer_group = "jac-scale"
serializer = "json"

[scale.events.retry]
max_attempts = 3
backoff_seconds = [1, 5, 30]
dead_letter_suffix = ".dlq"
```

The Postgres stream needs no extra packages: the driver is vendored with the runtime. With no database URL, scale uses `LocalEventStream` and logs a warning at startup.

### Publishing

```jac
import from jaclang.scale.events.publisher { publish }
import from jaclang.scale.events.broker { Event }

walker place_order {
    has order_id: int;
    has amount: float;

    can fire with Root entry {
        publish("orders.placed", Event(
            data={"order_id": self.order_id, "amount": self.amount},
            trace_id="trace-1"
        ));
    }
}
```

`publish()` is fire-and-forget. Errors from the broker are logged and swallowed so emit sites do not have to wrap calls in try/except. `event.event_type` auto-defaults to the topic when left empty, so the topic string only needs to appear once at the call site (set `event_type` explicitly only when it differs from the topic).

### Subscribing (push)

```jac
import from jaclang.scale.events.subscriber { subscribe }
import from jaclang.scale.events.broker { Event }

@subscribe("orders.placed")
def on_order_placed(event: Event) -> None {
    print(event.event_type, event.data);
}
```

Handlers register at import time. At server startup, the framework walks the registry and wires each handler into the active broker. A daemon consumer thread is spawned per subscription.

`@subscribe` accepts optional `group=` and `retry=` arguments to override the defaults from `jac.toml`, plus `start_from=` to control where a brand-new consumer group begins reading. Default is `"latest"` (only events produced after the group is created); pass `"earliest"` to replay everything still retained. The argument is a plain `str`; the two positions the shipped brokers understand are named by the `StreamPosition` enum in `jaclang.scale.events.broker`, so `start_from=StreamPosition.EARLIEST.value` says the same thing as `start_from="earliest"`. Any other token is treated as `"latest"`. `start_from` is a one-time bookmark: existing groups always resume from their stored position and ignore this argument.

```jac
@subscribe("orders.placed", start_from="earliest")
def replay_all(event: Event) -> None {
    print("replaying", event.id);
}
```

### Consuming (pull)

```jac
import from jaclang.scale.events.broker { EventStreamBroker }

def drain(broker: EventStreamBroker) -> int {
    batch = broker.consume(
        "orders.placed", max_messages=10, timeout_seconds=2.0
    );
    for ev in batch {
        # ... process ev ...
        broker.ack(ev);
    }
    return len(batch);
}
```

`consume()` blocks for up to `timeout_seconds` (default `5.0`) waiting for at least one event, then returns whatever has arrived (up to `max_messages`). Pass `timeout_seconds=0.0` for a non-blocking poll. Each event must be acked individually via `ack(event)` or the broker will redeliver it after its visibility timeout. `consume()` accepts the same `start_from=` argument as `subscribe()`; it only affects the first call that creates the consumer group, subsequent calls resume from the stored position.

Every broker honors that contract identically, whichever backend is selected: `EventStreamBroker` checks each implementation's method signatures against its own declarations when the subclass is defined, so a broker that drops a parameter, changes a default, or adds a required parameter raises `TypeError` at import instead of failing on a call at runtime. An implementation may widen with additional defaulted parameters.

### Configuration reference

| Key | Default | Description |
|-----|---------|-------------|
| `enabled` | `false` | Master switch. When `false`, all event-streaming calls are no-ops. |
| `url` | `null` | Postgres URL for the event stream. If unset, falls back to `[scale.database].url` / `JAC_DB_URL`. If nothing resolves, `LocalEventStream` (in-memory) is used. |
| `consumer_group` | `jac-scale` | Default consumer group name when `@subscribe` does not specify one. |
| `serializer` | `json` | Wire format. JSON only. |
| `retry.max_attempts` | `3` | Number of delivery attempts before sending to the DLQ topic. |
| `retry.backoff_seconds` | `[1, 5, 30]` | Backoff delays per attempt index, clamped to the last value. |
| `retry.dead_letter_suffix` | `.dlq` | Suffix appended to a topic name to form its dead-letter topic. |

### Reliability semantics

- **At-least-once delivery.** Handlers may run more than once for the same event. Make handlers idempotent, or dedupe on `event.id`.
- **Retry.** A failing handler is retried `retry.max_attempts` times with delays from `retry.backoff_seconds`. The thread sleeps responsively to the broker stop event so shutdowns are not blocked by long backoffs.
- **Dead-letter topic.** After retry exhaustion, the event is published to `<topic><retry.dead_letter_suffix>` and the original is acked so it is not redelivered indefinitely. The DLQ is a regular topic you can `consume()` like any other.
- **Drain on shutdown.** On process exit, consumer threads are signaled to stop and joined under a 10-second deadline.

### Operational notes

- Each subscription spawns one daemon consumer thread per topic/group. Inspect via standard threading tools.
- Delivery metadata is exposed as first-class fields on `Event`: `event.delivery_id`, `event.delivery_topic`, `event.delivery_group`. Handlers that need them for idempotency keys, structured logging, or dedup can read them directly without importing broker-specific constants. The fields are broker-managed: producers leave them `None`, the broker sets them on `consume()` / push delivery, and they are not serialized to the wire.
- Startup logs `Events broker enabled (kind={local|postgres}, subscriptions=N)` so it is easy to confirm wiring at a glance.
- The wire format is CloudEvents 1.0 valid (`specversion`, `type`, `data`, `id`, `source`, `time`, plus `trace_id` and `headers` as extensions), so strict CE consumers (Argo Events, Knative Eventing, CE-aware Kafka tooling) accept it.

---

## The Database

### One store, everywhere

Graph persistence is **Postgres-native** and there is exactly one stack:

- **Local development**: the runtime auto-provisions an embedded Postgres server, one database per project. `jac db status` / `jac db stop` manage it; nothing to install.
- **External server**: set `[scale.database].url` (or the `JAC_DB_URL` env var, which wins) and everything -- graph anchors, identity, scheduler jobs, webhook API keys, the event stream, and the WebSocket broadcast backplane -- runs against that database.
- **Kubernetes**: `jac scale deploy app.jac` provisions a Postgres StatefulSet with a PersistentVolumeClaim and injects `JAC_DB_URL` into every pod via a Kubernetes Secret. Subsequent deployments only update the application; the database remains untouched.

| `jac.toml` key (`[scale.database]`) | Default | Description |
|----------|---------|-------------|
| `url` | `null` | Postgres connection URL (`postgresql://user:pass@host:port/db`) for this process. `JAC_DB_URL` overrides it at runtime. Set here (not via the env var), it also makes a deploy of this app point at that database instead of provisioning one. |
| `deploy_mode` | `"image"` | How a provisioned Postgres runs: `"image"` (official postgres image) or `"embedded"` (the app's jac image running `jac db serve`). |
| `postgres_image` | `"postgres:18"` | Image used in `deploy_mode = "image"`. |
| `postgres_storage` | `"2Gi"` | PVC size for the provisioned StatefulSet. |
| `indexes` | `{}` | Archetype fields to index, as `{ Arch = ["field", ...] }`. Each named field is promoted to a generated `jsonb` column over `props->'archetype'->'<field>'` -- the same expression and type the query compiler compares against -- and indexed. Predicates, orderings and composite keys on a promoted field then name the column instead of the raw path, which is the difference between an index scan and a sequential one. |

### Promoted fields

A field predicate or an ordering term reads `props->'archetype'->'<field>'`. Postgres cannot use an index for that unless one exists over the same expression, so an unpromoted field is a scan of the joined set:

```toml
[scale.database]
indexes = { Msg = ["at", "seq"], Post = ["published"] }
```

Three things follow from the column being `jsonb` rather than `text`. Numbers order numerically, so `ORDER BY` and range predicates are correct without a cast. Row comparison works, which is what composite keyset pagination (`(at, seq) < (:a, :b)`) compiles to. And the column matches the comparison the compiler already emits, so promoting a field changes only which plan Postgres picks.

Promotion is an optimisation and never a semantic: an unpromoted field still filters, orders and paginates correctly, just by scanning. The index is created without a predicate so subtypes of the declared archetype are covered too. An existing promoted column of the wrong type is dropped and rebuilt on the next `ensure_schema`.

Deployment intent is a separate decision from runtime identity, and lives under `[scale.kubernetes]`:

| `jac.toml` key (`[scale.kubernetes]`) | Default | Description |
|----------|---------|-------------|
| `database_mode` | `"auto"` | What database the *deployed* app gets: `"provision"` (a per-app Postgres owned by jac), `"external"` (point it at `database_url` / `[scale.database]` `url`), `"none"` (wire no database), or `"auto"` (external when a url is configured, otherwise provision). |
| `database_url` | `""` | Connection URL handed to the deployed app in external mode. Highest precedence, above `[scale.database]` `url`. |
| `database_namespace` | `""` | Namespace that runs the external database service, used to qualify a bare service name when the app deploys into a different namespace. |

The precedence for a deploy is `[scale.kubernetes]` `database_mode` / `database_url`, then `[scale.database]` `url`, then provision a per-app Postgres. The **deploying process's own `JAC_DB_URL` is never consulted**: it means "the database this process connects to", which is a different fact from "the database the deployed app should connect to". A platform service that deploys tenant apps therefore keeps its own database and still gets one provisioned per app.

A bare Kubernetes service name in an external URL only resolves inside the namespace that owns it. When the deploy targets a different namespace, jac qualifies the host to `<service>.<namespace>.svc.cluster.local` using `database_namespace` (or the deploying pod's own namespace, read from `POD_NAMESPACE` or the ServiceAccount namespace file). When neither is available the deploy fails rather than emitting a manifest whose database host cannot resolve.

`postgres_enabled` is deprecated: `false` maps to `database_mode = "none"`, and `true` is ignored because a boolean cannot distinguish provisioning from pointing at an external database.

Credentials are never hardcoded in pod specs: the provisioned password lives in a Kubernetes `Secret` (`{app}-postgres-secret`) and pods receive `JAC_DB_URL` via `valueFrom.secretKeyRef`.

#### Server tuning

Every Postgres jac starts -- the embedded server, `jac db serve`, and the provisioned StatefulSet -- is started with the same three settings, because jac's write transactions are `SERIALIZABLE` and stock Postgres is not sized for that:

| Setting | Value | Why |
|---------|-------|-----|
| `max_connections` | `256` | Absorbs a default `jac test` fan-out plus interactive commands without starving either. |
| `max_pred_locks_per_transaction` | `1024` | SERIALIZABLE takes one SIReadLock per row/page read, and the pool is `max_pred_locks_per_transaction` x `max_connections` entries -- 262,144 here, against a stock 64 x 100 = 6,400. Overflowing the pool fails **every** statement with `53200 out of shared memory ... CreatePredicateLock`, not just the transaction that overran it. |
| `idle_in_transaction_session_timeout` | `300000` (5 min) | Disconnects any process that regresses on the no-idle-transaction invariant instead of letting it pin predicate-lock reclamation cluster-wide. The store heals the resulting `25P03` transparently. |

If you point `[scale.database].url` at a managed Postgres, set `max_pred_locks_per_transaction` there yourself; keep the product of it and `max_connections` comfortably above the peak SIReadLock count your workload holds (`SELECT count(*) FROM pg_locks WHERE mode = 'SIReadLock'`).

### Consistency model

There is no cache tier and no cross-pod invalidation protocol to configure: each request's unit of work reads and writes inside one `SERIALIZABLE` Postgres transaction, and **the transaction is the single source of truth**. Racing requests converge via abort-and-replay; see [Persistence -> Concurrent writes](../persistence.md#concurrent-writes-check-then-create-and-convergence). The infrastructure reads that precede your code (resolving the request context's root anchor, and health probes) run in a declared `REPEATABLE READ READ ONLY` transaction that takes no predicate locks, and the transaction is restarted at SERIALIZABLE before a unit of work can write. Cross-pod signaling (WebSocket broadcasts, event delivery) rides Postgres `LISTEN`/`NOTIFY` on the same database.

The admin dashboard's Ops page (`/admin/ops`) renders a Postgres health card driven by a live `SELECT 1` probe, so a database incident is visible without kubectl.

---

## Builtins

### Root Access

```jac
with entry {
    # Get all roots in memory/database
    roots = allroots();
}
```

### Memory Commit

```jac
with entry {
    # Commit memory to database
    commit();
}
```

---
